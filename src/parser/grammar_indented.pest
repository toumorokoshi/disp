head = _{ list_of_lines ~ EOI }
list_of_lines = {
  expression_no_parens ~ ("\n" ~ PEEK_ALL ~ expression_no_parens?)*
}
expression_no_parens = {
  (comment | (token ~ delimiter_no_newline?)+) ~ ("\n" ~ indented_list)?
}
// TODO: add comments to syntax tree ,needed to preserve comments during
// transforms.
comment = { "#" ~ comment_body }
indented_list = _{ PEEK_ALL ~ PUSH(indentation) ~ list_of_lines ~ DROP }
delimiter = _{ (delimiter_no_newline | "\n" | "\t")+ }
delimiter_no_newline = _{ (" ")+ }
indentation = _{ ("\t")+ }
token = _{
  none | expression | integer | list | map |
  // ordering is important! symbol must come after
  // any literals in the parser, or symbol will match first.
  // e.j. "true" will be a symbol if the rule is after symbol.
  string_with_quotes | bytes_with_quotes | bang_symbol_with_bang |
  true_value | false_value | symbol
}
none = { "None" }
expression = { "(" ~ delimiter? ~ token_list ~ delimiter? ~ ")"}
integer = { ('0'..'9')+ }
list = {"[" ~ delimiter? ~ token_list? ~ delimiter? ~ "]"}
map = { "{" ~ delimiter? ~ (token ~ delimiter? ~ ":" ~ delimiter? ~ token ~ delimiter? ~ "," ~ delimiter?)* ~ "}" }
string_with_quotes = _{ "\"" ~ string ~ "\""}
bytes_with_quotes = _{ "'" ~ bytes ~ "'"}
bang_symbol = { symbol }
bang_symbol_with_bang = _{ bang_symbol ~ "!"}
true_value = { "true" }
false_value = { "false" }
symbol = { ('a'..'z' | 'A'..'Z' | "+" | "=" | "-")+ }

string = { char+ }
bytes = { char+ }
char = _{ ('a'..'z' | 'A'..'Z' | '0'..'9' | "#" | " " | ":" | "," | "." | "-" | "(" | ")" | "["|"]" | "_" | "!" | "{"|"}")+ }
comment_body = _{ (char | "\"")+ }
token_list = _{ (token ~ delimiter?)+ }
